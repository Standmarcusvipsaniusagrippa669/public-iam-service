plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.flywaydb.flyway' version '10.14.0'
	id 'jacoco'
    id 'org.graalvm.buildtools.native' version '0.11.1'

}

group = 'com.valiantech'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    all {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'com.bucket4j:bucket4j_jdk17-core:8.14.0'
	implementation 'com.bucket4j:bucket4j_jdk17-jedis:8.14.0'
	implementation 'com.bucket4j:bucket4j_jdk17-redis-common:8.14.0'
	implementation 'redis.clients:jedis:5.1.3'
	implementation 'org.flywaydb:flyway-core:10.14.0'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'io.hypersistence:hypersistence-utils-hibernate-60:3.9.0'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'com.sendgrid:sendgrid-java:4.10.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'com/valiantech/core/iam/config/**',
					'com/valiantech/core/iam/user/model/**',
					'com/valiantech/core/iam/user/repository/**',
					'com/valiantech/core/iam/company/model/**',
					'com/valiantech/core/iam/company/repository/**',
					'com/valiantech/core/iam/invitation/model/**',
					'com/valiantech/core/iam/invitation/repository/**',
					'com/valiantech/core/iam/usercompany/repository/**',

			])
		}))
	}
}

bootBuildImage {
    imageName = "valiantech/${project.name}:latest"
    environment = [
            "BP_JVM_VERSION"     : "21",
            "BP_NATIVE_IMAGE"    : "false",
            "BP_OCI_LABELS"      : "maintainer=valiantech"
    ]
}